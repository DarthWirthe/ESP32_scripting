
int button_pin = 15 // Кнопка
int updateTime = 0, frameTime = 0, fctr = 0, fps = 0
global int circle_size = 10, circles_count = 50 // радиус и количество фигур
int arr_size = circles_count * 4
global float circles[arr_size] // Массив координат фигур

for int i = 0; i < arr_size; i = i + 1
{
 circles[i] = 0
}

// Функция для расположения фигур по центру
function init_pos()
{
 float x = tft.width() / 2
 for int i = 0; i < circles_count; i = i + 1
 {
  int pos = i * 4
  circles[pos] = x
 }
}

// Функция симуляции физики
function physics()
{
 int w = tft.width(), h = tft.height()
 for int i = 0; i < circles_count; i = i + 1
 {
  int pos = i * 4
  float x = circles[pos], y = circles[pos + 1], movex = circles[pos + 2], movey = circles[pos + 3]
  if x < 0 {x = 0; movex = -(movex / 1.2)}
  if x > w - circle_size {x = w - circle_size; movex = -(movex / 1.2)}
  if y < 0 {y = 0; movey = -(movey / 1.2)}
  if y > h - circle_size {y = h - circle_size; movey = -(movey / 1.2)}
  circles[pos] = x + movex
  circles[pos + 1] = y + movey
  circles[pos + 2] = movex
  circles[pos + 3] = movey + 0.05
 }
}

// Функция отрисовки кадра
function draw()
{
 tft.fill_screen(tft.rgb(255, 255, 200))
 for int i = 0; i < circles_count; i = i + 1
 {
  int pos = i * 4
  float x = circles[pos], y = circles[pos + 1]
  tft.fill_circle(x, y, circle_size, tft.rgb(200, 16, 16))
  tft.fill_circle(x + circle_size / 2, y - circle_size / 2, circle_size / 4, tft.rgb(255, 255, 255))
 }
}

gpio.set_mode_input(button_pin)
tft.init_buffered(1)
init_pos()

// Основной цикл
while true
{
 if (updateTime <= esp.timer())
 {
  updateTime = esp.timer() + 20000
  physics()
  draw()
  tft.set_cursor(1, 1)
  tft.write("fps: ")
  tft.write(fps)
  tft.draw_buffer()
  // Нажатие кнопки толкает фигуры вверх и в стороны
  if (gpio.read(button_pin)) {
   for int n = 0; n < circles_count; n = n + 1
   {
    int pos = n * 4
    circles[pos + 2] = circles[pos + 2] + 0.2 * (10 - m.random(1, 20))
    circles[pos + 3] = circles[pos + 3] - 0.1 * m.random(1, 5)
   }
  }
  fctr = fctr + 1
 }
 // Счетчик fps
 if (frameTime <= esp.timer())
 {
  frameTime = esp.timer() + 1000000
  fps = fctr
  fctr = 0
 }
}